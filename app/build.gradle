/*
 * Copyright 2016 Epishie
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def secret = new Properties()
secret.load(new FileInputStream(rootProject.file("secret.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BASE_URL", "\"https://oauth.reddit.com\""
        buildConfigField "String", "TOKEN_URL", "\"https://www.reddit.com\""
        buildConfigField "String", "OAUTH_CLIENT_ID", "\"${secret.redditClientId}\""
    }
    signingConfigs {
        release {
            storeFile rootProject.file(secret.keyStore)
            storePassword secret.keyStorePassword
            keyAlias secret.keyAlias
            keyPassword secret.keyAliasPassword
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        alpha {
            applicationId "com.epishie.tabs"
            manifestPlaceholders = [HOCKEYAPP_APP_ID: secret.hockeyAppId]
        }
        production {
            applicationId "com.epishie.tabs"
            manifestPlaceholders = [HOCKEYAPP_APP_ID: ""]
        }
    }
}

ext {
    supportLibVersion = '23.2.1'
    daggerVersion = '2.1'
    rxjavaVersion = '1.1.2'
    rxandroidVersion = '1.1.0'
    okhttpVersion = '3.2.0'
    retrofitVersion = '2.0.0'
    gsonVersion = '2.6.2'
    picassoVersion = '2.5.2'
    junitVersion = '4.12'
    assertjVersion = '1.1.1'
    robolectricVersion = '3.0'
    mockitoVersion = '1.10.19'
    hockeyVersion = '3.7.2'
}

dependencies {
    // ANDROID
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:percent:${supportLibVersion}"

    // DEPENDENCY INJECTION
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided 'javax.annotation:jsr250-api:1.0'

    // REACTIVE
    compile "io.reactivex:rxjava:${rxjavaVersion}"
    compile "io.reactivex:rxandroid:${rxandroidVersion}"

    // RETROFIT
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-scalars:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"

    // JSON
    compile "com.google.code.gson:gson:${gsonVersion}"

    // IMAGE
    compile "com.squareup.picasso:picasso:${picassoVersion}"

    // UNIT TEST
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.robolectric:robolectric:${robolectricVersion}"
    testCompile "org.robolectric:shadows-support-v4:${robolectricVersion}"

    // ASSERTIONS
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile ("com.squareup.assertj:assertj-android:$assertjVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    testCompile ("com.squareup.assertj:assertj-android-support-v4:$assertjVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    testCompile ("com.squareup.assertj:assertj-android-design:$assertjVersion@aar") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    testCompile ("com.squareup.assertj:assertj-android-recyclerview-v7:$assertjVersion@aar") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
    }

    // MOCKS
    testCompile "org.mockito:mockito-core:${mockitoVersion}"

    // TEST
    alphaCompile "net.hockeyapp.android:HockeySDK:${hockeyVersion}"
}
